STARTUP(LIBC_PATH/arch/UARCH/src/entry.o)
ENTRY(__entry)

PHDRS {
#ifdef LOADER
	interp PT_INTERP;
#endif
	text PT_LOAD FILEHDR PHDRS FLAGS(5);
	data PT_LOAD FLAGS(6);
}

SECTIONS {
#ifdef LOADER
	. = SEGMENT_START("text-segment", 0x70400000);
#else
	. = SEGMENT_START("text-segment", 0x400000);
#endif
#ifndef SHLIB
	PROVIDE (__executable_start = .);
#endif
	. = . + SIZEOF_HEADERS;

	.init : {
		*(.init);
	} :text

	.text : {
		*(.text .text.*);
		*(.rodata .rodata.*);
	} :text

#ifdef LOADER
	.interp : {
		*(.interp);
	} :interp :text
#endif

	. = . + 0x1000;

	.data : {
		*(.opd);
		*(.data .data.*);
		*(.sdata);
	} :data

	.tdata : {
		_tdata_start = .;
		*(.tdata);
		_tdata_end = .;
		_tbss_start = .;
		*(.tbss);
		_tbss_end = .;
	} :data

	_tls_alignment = ALIGNOF(.tdata);

	.bss : {
		*(.sbss);
		*(.scommon);
		*(COMMON);
		*(.bss);
	} :data

	/DISCARD/ : {
		*(*);
	}
}
