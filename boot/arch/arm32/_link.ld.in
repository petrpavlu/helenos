#include <arch/arch.h>

ENTRY(start)

SECTIONS {
	. = BOOT_BASE;
	.text : {
		loader_start = .;
		*(BOOTSTRAP);
		*(.text);
	}
	. = BOOT_BASE + 0x8000;
	.data : {
		*(BOOTPT);      /* bootstrap page table */
		*(BOOTSTACK);   /* bootstrap stack */
		*(.data);       /* initialized data */
		*(.rodata);
		*(.rodata.*);
		*(.sdata);
		*(.reginfo);
		*(.sbss);
		*(.scommon);
		*(.bss);        /* uninitialized static variables */
		*(COMMON);      /* global variables */
		loader_end = .;
		payload_start = .;
		*(.payload);
		payload_end = .;
	}

	/DISCARD/ : {
		*(.gnu.*);
		*(.ARM.*);
		*(.mdebug*);
		*(.pdr);
		*(.comment);
		*(.note.*);
	}
}
