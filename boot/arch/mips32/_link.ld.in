ENTRY(start)

SECTIONS {
#if defined(MACHINE_msim)
	. = 0xbfc00000;
#elif defined(MACHINE_lmalta) || defined(MACHINE_bmalta)
	. = 0x80103000;
#endif
	.text : {
		loader_start = .;
		*(BOOTSTRAP);
		*(.text);
	}
	.data : {
		*(.data);       /* initialized data */
		*(.rodata);
		*(.rodata.*);
		*(.sdata);
		*(.reginfo);
		*(.sbss);
		*(.scommon);
		*(.bss);        /* uninitialized static variables */
		*(COMMON);      /* global variables */
		loader_end = .;
		payload_start = .;
		*(.payload);
		payload_end = .;
	}

	/DISCARD/ : {
		*(.gnu.*);
		*(.mdebug*);
		*(.pdr);
		*(.comment);
		*(.note);
	}
}
