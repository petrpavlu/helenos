/** PPC32 linker script
 *
 * umapped section:
 *  kernel text
 *  kernel data
 * mapped section:
 *  kernel text
 *  kernel data
 *
 */

#include <arch/boot/boot.h>
#include <arch/mm/page.h>

ENTRY(kernel_image_start)
OUTPUT_FORMAT("elf32-powerpc")
OUTPUT_ARCH("powerpc:common")

SECTIONS {
	kernel_load_address = PA2KA(0);

	.unmapped (SIZEOF_HEADERS): AT (SIZEOF_HEADERS) {
		. = ALIGN(0x100);
		*(K_UNMAPPED_TEXT_START);
	}

	.mapped PA2KA(BOOT_OFFSET): AT (BOOT_OFFSET) {
		ktext_start = .;
		*(K_TEXT_START);
		*(.text);
		ktext_end = .;

		kdata_start = .;
		*(K_DATA_START);
		*(.rodata .rodata.*);
		*(.data);       /* initialized data */
		*(.sdata);
		*(.sdata2);
		*(.sbss);
		*(.bss);        /* uninitialized static variables */
		*(COMMON);      /* global variables */

		. = ALIGN(8);
		symbol_table = .;
		*(symtab.*);    /* Symbol table, must be LAST symbol!*/

		kdata_end = .;
	}

	/DISCARD/ : {
		*(*);
	}
}
