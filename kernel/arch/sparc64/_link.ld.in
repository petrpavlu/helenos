/** SPARC64 linker script
 *
 *  It is ELF format, but its only section looks like this:
 *   kernel text
 *   kernel data
 *
 */

#include <arch/boot/boot.h>

ENTRY(kernel_image_start)

SECTIONS {
	kernel_load_address = VMA;

	.image (VMA + SIZEOF_HEADERS): AT (LMA + SIZEOF_HEADERS) {
		ktext_start = .;
		*(K_TEXT_START)
		*(.text .text.*);
		ktext_end = .;

		kdata_start = .;
		*(K_DATA_START)
		*(.rodata .rodata.*);
		*(.data);                   /* initialized data */
		*(.sdata);
		*(.sdata2);
		*(.sbss);
		. = ALIGN(8);
		*(.bss);                    /* uninitialized static variables */
		*(COMMON);                  /* global variables */

		. = ALIGN(8);
		symbol_table = .;
		*(symtab.*);                /* Symbol table, must be LAST symbol!*/

		kdata_end = .;
	}

	/DISCARD/ : {
		*(*);
	}

}
